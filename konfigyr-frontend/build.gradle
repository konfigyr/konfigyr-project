import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage

plugins {
    id 'com.github.node-gradle.node' version '7.1.0'
    id 'com.bmuschko.docker-remote-api' version '10.0.0'
    id 'org.springframework.boot'
}

docker {
    registryCredentials {
        url = 'https://index.docker.io/v1/'
        username = 'konfigyr'
        password = System.getenv('DOCKER_HUB_TOKEN')
    }
}

node {
    npmCommand = String.valueOf(getProperty('node.npm'))

    /* use the CI NPM command when executing Github actions */
    npmInstallCommand = System.getenv('CI') ? 'ci' : 'install'
}

tasks.register('npmBuild', NpmTask) {
    dependsOn npmInstall

    description = 'Builds the frontend application'

    args = ['run', 'build']

    /* define inputs for the task in order to run only when the inputs change */
    inputs.files('package.json', 'package-lock.json', 'vite.config.ts', 'tsconfig.json')
    inputs.dir('src')
    inputs.dir('messages')
    inputs.dir('public')
    inputs.dir(fileTree('node_modules').exclude('.cache'))

    /* mark the dist as the output directory */
    outputs.dir(project.layout.buildDirectory.dir('.output'))
}

tasks.register('npmTest', NpmTask) {
    dependsOn npmInstall

    description = 'Runs the frontend application tests'

    args = System.getenv('CI') ? ['run', 'ci'] : ['test']

    /* define inputs for the test task in order to run only when the related files change */
    inputs.files('package.json', 'package-lock.json', 'eslint.config.mjs', 'vitest.config.mts')
    inputs.dir('src')
    inputs.dir('messages')
    inputs.dir('public')
    inputs.dir('test')
}

tasks.register('dockerBuild', DockerBuildImage) {
    dependsOn bootJar

    group = 'docker'
    description = 'Builds the Docker image with a frontend application'

    def tag = Boolean.valueOf(System.getenv('NIGHTLY')) ? 'latest' : project.version

    inputDir.set(project.layout.projectDirectory)
    images.add("konfigyr/${project.name}:${tag}")
}

tasks.register('dockerPublish', DockerPushImage) {
    dependsOn dockerBuild

    group = 'docker'
    description = 'Builds and publishes the Docker image with a frontend application'

    def tag = Boolean.valueOf(System.getenv('NIGHTLY')) ? 'latest' : project.version

    images.add("konfigyr/${project.name}:${tag}")
}

/**
 * Disable Spring Boot tasks and make sure that `bootBuildImage` is finalized
 * by the `dockerPublish` task in order to publish the Frontend Docker image
**/

tasks.named('bootJar') {
    enabled(false)
}

tasks.named('bootBuildImage') {
    enabled(false)

    finalizedBy dockerPublish
}

tasks.assemble.dependsOn('npmBuild')
tasks.test.dependsOn('npmTest')
