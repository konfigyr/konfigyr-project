plugins {
    id 'io.freefair.lombok' version '8.6' apply false
    id 'org.springframework.boot' version '3.3.3' apply false
    id 'io.spring.dependency-management' version '1.1.5' apply false
}

subprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'checkstyle'
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'io.spring.dependency-management'

    repositories {
        mavenCentral()
        maven { url 'https://repo.spring.io/milestone' }
        mavenLocal()
    }

    group = 'com.konfigyr'
    version = '0.0.1'

    dependencies {
        annotationProcessor 'org.springframework.boot:spring-boot-autoconfigure-processor'
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

        compileOnly 'com.google.code.findbugs:jsr305:3.0.2'

        checkstyle 'io.spring.javaformat:spring-javaformat-checkstyle:0.0.42'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${findProperty('versions.spring.boot')}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${findProperty('versions.spring.cloud')}"
            mavenBom "org.springframework.modulith:spring-modulith-bom:${findProperty('versions.spring.modulith')}"
            mavenBom "org.jmolecules:jmolecules-bom:${findProperty('versions.spring.jmolecules')}"
        }

        dependencies {
            /* Konfigyr Libraries */
            dependency('com.konfigyr:konfigyr-artifactory:1.0.0-RC1')
            dependency('com.konfigyr:konfigyr-mail-api:1.0.0-RC1')
            dependency('com.konfigyr:konfigyr-mail-smtp:1.0.0-RC1')
            dependency('com.konfigyr:konfigyr-mail-thymeleaf:1.0.0-RC1')
            dependency('com.konfigyr:konfigyr-crypto-api:1.0.0-RC2')
            dependency('com.konfigyr:konfigyr-crypto-tink:1.0.0-RC2')
            dependency('com.konfigyr:konfigyr-crypto-jdbc:1.0.0-RC2')

            /* Fix Dependabot alerts */
            dependency('org.apache.commons:commons-compress:1.26.2') // used by testcontainers
            dependency('com.google.guava:guava:33.2.1-jre') // used by checkstyle
        }
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    test {
        useJUnitPlatform()
    }

    jacocoTestReport {
        reports {
            xml.required = true
            html.required = false
        }
    }

    tasks.withType(Javadoc).configureEach {
        options.addStringOption('Xdoclint:-missing', '-quiet')
    }

    check.dependsOn jacocoTestReport
}
